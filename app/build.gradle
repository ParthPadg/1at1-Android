buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.0' // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'

def major = 1
def minor = 0
def patch = 6

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        buildToolsVersion "25.0.2"
        applicationId "org.oneat1.android"
        minSdkVersion 19
        compileSdkVersion 25
        targetSdkVersion 25
        versionCode major * 10000 + minor * 1000 + patch * 10
        versionName "${major}.${minor}.${patch}"
        resConfigs "en"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            ext.enableCrashlytics = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig

        }
        debug {
            ext.enableCrashlytics = false
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

play{
    jsonFile = file("../uploadKey.json")
    track = "alpha"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //Support Libraries
    def supportLib = "25.1.0"
    compile "com.android.support:support-v4:$supportLib"
    compile "com.android.support:support-v13:$supportLib"
    compile "com.android.support:design:$supportLib"
    compile "com.android.support:cardview-v7:$supportLib"

    def firebase = "10.0.1"
    compile "com.google.firebase:firebase-messaging:$firebase"
    compile "com.google.firebase:firebase-config:$firebase"

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'com.github.tony19:logback-android-classic:1.1.1-6'
    compile 'com.balysv:material-ripple:1.0.2'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0' //OkHTTP transitively obtained from Twitter/Fabric

    compile('com.twitter.sdk.android:twitter:2.3.1@aar') { transitive = true; };
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') { transitive = true; }
}

apply plugin: 'com.google.gms.google-services'