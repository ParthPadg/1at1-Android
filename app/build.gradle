buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.0' // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin

    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def major = 2
def minor = 0
def patch = 0

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        buildToolsVersion "25.0.2"
        applicationId "org.oneat1.android"
        minSdkVersion 19
        compileSdkVersion 25
        targetSdkVersion 25
        versionCode major * 10000 + minor * 1000 + patch * 10
        versionName "${major}.${minor}.${patch}"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            ext.enableCrashlytics = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig

        }
        debug {
            ext.enableCrashlytics = false
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //Support Libraries
    def supportLib = "25.3.1"
    compile "com.android.support:support-v4:$supportLib"
    compile "com.android.support:support-v13:$supportLib"
    compile "com.android.support:design:$supportLib"
    compile "com.android.support:cardview-v7:$supportLib"

    def firebase = "10.2.1"
    compile "com.google.firebase:firebase-messaging:$firebase"
    compile "com.google.firebase:firebase-config:$firebase"

    def butterknifeVersion = '8.5.1'
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'com.github.tony19:logback-android-classic:1.1.1-6'
    compile 'com.balysv:material-ripple:1.0.2'

    compile "com.google.code.gson:gson:2.8.0"

    compile('com.twitter.sdk.android:twitter:2.3.2@aar') { transitive = true; };
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') { transitive = true; }

    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0' //OkHTTP transitively obtained from Twitter/Fabric

    compile "io.reactivex.rxjava2:rxjava:2.0.8"
    compile("io.reactivex.rxjava2:rxandroid:2.0.1") { exclude module: "rxjava" }
    compile "com.squareup.retrofit2:adapter-rxjava2:2.2.0"

}

apply plugin: 'com.google.gms.google-services'